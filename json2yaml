#!/usr/bin/env perl
#
# $Id: json2yaml,v 1.13 2011/06/10 05:07:41 ryo Exp $
#
# Copyright(c) 2009 Ryo SHIMIZU <ryo@misakimix.org>
#

use strict;
use warnings;
use Encode;
use JSON::XS;
use YAML::XS;
use LWP::Simple;

if ($#ARGV >= 0) {
	for my $file (@ARGV) {
		print "== $file ==\n" if ($#ARGV > 0);
		json2yaml(scalar(fileread($file)));
	}
} else {
	my $file = join("", <>);
	json2yaml($file);
}
exit;

sub json2yaml {
	my $json = shift;

	# XXX: json log hack
	if ($json =~ m/,\n$/s) {
		$json =~ s/,\n$//s;
		$json = '[' . $json . ']';
	}

	my $data;
	eval('$data = decode_json($json)');
	unless (defined $data) {
		$json = '[' . join(",", split(/\n/, $json)) . ']';
		eval('$data = decode_json($json)');
	}

	print YAML::XS::Dump($data);
}

sub fileread {
	my $file = shift;

	if ($file =~ m#^(http|https|ftp)://#i) {
		my $json = get($file);
		unless (defined $json) {
			warn "$file: error\n";
			$json = '{}';
		}
		$json;
	} else {
		open my $fh, "<", $file or die "open: $file: $!\n";
		local $/;
		undef $/;
		<$fh>;
	}
}
